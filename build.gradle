plugins {
    id 'fabric-loom' version '1.10-SNAPSHOT'
    id 'me.modmuss50.mod-publish-plugin' version '0.8.+'
}

version = project.mod_version
group = project.maven_group

base {
    archivesName = project.mod_id
}

repositories {
}

dependencies {
    // To change the versions see the gradle.properties file
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    // Fabric API. This is technically optional, but you probably want it anyway.
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
}

loom {
    splitEnvironmentSourceSets()
    mixin.useLegacyMixinAp = false
}

import groovy.json.JsonSlurper
import groovy.json.JsonOutput

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }

    doLast {
        filesMatching("**/*.json") {
            def file = it.file
            file.text = JsonOutput.toJson(new JsonSlurper().parse(file))
        }
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.release = 17
}

java {
    withSourcesJar()
    sourceCompatibility = targetCompatibility = JavaVersion.VERSION_17
}

jar {
    from("LICENSE") {
        rename { "${it}_${mod_id}" }
    }
}

publishMods {
    file = jar.archiveFile
    additionalFiles.from sourcesJar
    type = STABLE
    //TODO: DONT FORGET TO UPDATE THIS
    changelog = project.changelog
    modLoaders.add 'fabric'
    displayName = mod_id + ' ' + mod_version
    version = "$mod_version+$minecraft_version"

    modrinth {
        projectId = 'xdTrASAc'
        accessToken = providers.environmentVariable 'MODRINTH_TOKEN'
        minecraftVersions.addAll minecraft_version
    }

    curseforge {
        projectId = '1153601'
        accessToken = providers.environmentVariable 'CURSEFORGE_TOKEN'
        minecraftVersions.addAll minecraft_version
        clientRequired = false
    }

    github {
        accessToken = providers.environmentVariable 'GITHUB_TOKEN'
        repository = 'AnAwesomGuy/taking-a-break'
        tagName = mod_version
        commitish = 'master'
    }
}